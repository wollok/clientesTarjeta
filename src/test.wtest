import clientes.*

describe "tests de clientes" {

	const clienteSafeShop = new Cliente()
	const clienteSafePromo = new Cliente() 

	fixture {
		clienteSafeShop.agregarCondicionComercial(safeShop)
		clienteSafePromo.agregarCondicionComercial(safeShop)
		clienteSafePromo.agregarCondicionComercial(promocion)
	}
			
	test "cliente con safe shop compra por poco bien" {
		clienteSafeShop.comprar(10)
		assert.equals(10, clienteSafeShop.deuda())
	}

	test "cliente con safe shop no puede comprar por mucho" {
		assert.throwsExceptionWithMessage("Debe comprar por menos de 50", { clienteSafeShop.comprar(150) })
	}

	test "cliente con safe shop y promoción no puede comprar por mucho" {
		assert.throwsExceptionWithMessage("Debe comprar por menos de 50", { clienteSafePromo.comprar(150) })
		assert.equals(0, clienteSafePromo.puntosPromocion())
	}

	test "cliente con safe shop y promoción compra, valida y suma puntos promo" {
		clienteSafePromo.comprar(25)
		assert.equals(15, clienteSafePromo.puntosPromocion())
	}
	
	// TODO: Qué pasa si el cliente tiene promoción primero y luego safe shop,
	// y quiere comprar por 50
}

